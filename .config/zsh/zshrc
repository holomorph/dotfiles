# ~/.config/zsh/zshrc

zmodload zsh/complist
autoload -Uz compinit && compinit
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search

setopt auto_cd correct
setopt extended_glob append_history extended_history share_history
setopt hist_find_no_dups hist_expire_dups_first hist_ignore_dups
setopt hist_ignore_space hist_reduce_blanks hist_save_no_dups hist_verify
setopt prompt_subst
unsetopt flow_control

HISTSIZE=20000
SAVEHIST=10000
HISTFILE="$HOME/.logs/zhistory"

## configs
for cfg in aliases bindings extract fun style; do
	[[ -e "$HOME/.config/zsh/$cfg.zsh" ]] && \
		source "$HOME/.config/zsh/$cfg.zsh"
done
unset cfg

## prompt
if [[ -s "$HOME/.config/zsh/prompt.zsh" ]]; then
	source "$HOME/.config/zsh/prompt.zsh"
else
	PROMPT="%(?..%B%F{red}exit %?%f%b
)%B%F{black}[%b%f"\
'%F{11}%~%B%F{black}]%b%f'\
'%(!.%F{red}#%f.%F{blue}$%f) '
fi
SPROMPT="Correct %B%F{red}%R%b%f to %B%F{green}%r%b%f [nyae]? "

## hooks
precmd() {
	print -Pn "\e];%m (%~) - Terminal\a"
}

preexec() {
	local cmd=${1[(wr)^(*=*|sudo|-*)]}
	print -Pn "\e];$cmd:q - Terminal\a"
}
